# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=eventlet
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.25.1
pkgrel=2
pkgdesc="Highly concurrent networking library (mingw-w64)"
arch=('any')
url='https://eventlet.net/'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-greenlet"
         "${MINGW_PACKAGE_PREFIX}-python3-monotonic")
optdepends=("${MINGW_PACKAGE_PREFIX}-python3-psycopg2: non-blocking PostgreSQL support"
            "${MINGW_PACKAGE_PREFIX}-python3-pyopenssl: non-blocking SSL support"
            "${MINGW_PACKAGE_PREFIX}-python3-httplib2: non-blocking HTTP support"
            "${MINGW_PACKAGE_PREFIX}-python3-pyzmq: non-blocking ZeroMQ support"
            "${MINGW_PACKAGE_PREFIX}-python3-dnspython: non-blocking DNS support")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-psycopg2"
              "${MINGW_PACKAGE_PREFIX}-python3-nose"
              "${MINGW_PACKAGE_PREFIX}-python3-pyopenssl"
              "${MINGW_PACKAGE_PREFIX}-python3-httplib2"
              "${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-pyzmq"
              "${MINGW_PACKAGE_PREFIX}-python3-dnspython")
options=('staticlibs' 'strip' '!debug')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/eventlet/eventlet/archive/v${pkgver}.tar.gz)
sha512sums=('ed3c1bfc17039c17730d4a8bb8ae3a3604b0210f3e6c326764fce748d7466a090b270c5dbc713aaf023681f33a80967adbe83f6280b61f5661a309a0bd43249a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' \
      -i tests/*_test.py

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build 
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/nosetests -sv tests || warning "tests failed"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
