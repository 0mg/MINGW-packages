# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=u-msgpack
pkgbase=mingw-w64-python-${_realname}
_pkgname=${_realname}-python
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.5.2
pkgrel=2
pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python (mingw-w64)"
arch=('any')
url='https://github.com/vsergeev/${_pkgname}'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/vsergeev/u-msgpack-python/archive/v$pkgver.tar.gz")
sha256sums=('685d9f7dfbee8c840778d866d30a6b8c335b749b7c8716290b906a22e7fd29c4')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
