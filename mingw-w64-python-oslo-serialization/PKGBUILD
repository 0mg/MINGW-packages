# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-serialization
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.29.2
pkgrel=2
pkgdesc="Oslo Serialization library (mingw-w64)"
arch=('any')
url='https://pypi.org/project/oslo.serialization/'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-six" 
         "${MINGW_PACKAGE_PREFIX}-python3-pbr"
         "${MINGW_PACKAGE_PREFIX}-python3-babel"
         "${MINGW_PACKAGE_PREFIX}-python3-msgpack"
         "${MINGW_PACKAGE_PREFIX}-python3-oslo-utils"
         "${MINGW_PACKAGE_PREFIX}-python3-pytz")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('62d18c582406d5e0d4c8927d0a598f819744d3ebf11065aa88689d963c7fc3ba0234897b61d714646ca908e181b5005f97acf098407aa021ea757ebb3e070082')

prepare() {
  cd "${srcdir}/oslo.serialization-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  # set version for pbr
  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/oslo.serialization-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/oslo.serialization-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py testr || warning "Tests failed"
}

package() {
  cd "${srcdir}/oslo.serialization-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
