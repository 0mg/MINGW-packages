# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cryptography
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.8
pkgrel=2
pkgdesc="A package designed to expose cryptographic recipes and primitives to Python developers (mingw-w64)"
url='https://pypi.org/project/cryptography/'
license=('Apache')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-python3-cffi"
         "${MINGW_PACKAGE_PREFIX}-python3-pyasn1"
         "${MINGW_PACKAGE_PREFIX}-python3-idna"
         "${MINGW_PACKAGE_PREFIX}-python3-asn1crypto")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-six")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest-runner"
              #"${MINGW_PACKAGE_PREFIX}-python3-iso8601"
              "${MINGW_PACKAGE_PREFIX}-python3-pretend"
              #"${MINGW_PACKAGE_PREFIX}-python3-cryptography-vectors"
              #"${MINGW_PACKAGE_PREFIX}-python3-hypothesis"
              "${MINGW_PACKAGE_PREFIX}-python3-pytz")
source=(https://pypi.io/packages/source/c/cryptography/${_realname}-${pkgver}.tar.gz)
sha256sums=('3cda1f0ed8747339bbdf71b9f38ca74c7b592f24f65cdb3ab3765e4b02871651')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} python3-build-${CARCH}
}

build() {
  msg "Python 3 build for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd ${srcdir}/python3-build-${CARCH}
  ${MINGW_PREFIX}/bin/python3 setup.py build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
