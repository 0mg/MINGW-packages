# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=absl-py
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.8.1
pkgrel=2
pkgdesc="Abseil Common Libraries (Python) (mingw-w64)"
arch=('any')
url="https://github.com/abseil/abseil-py"
license=('Apache 2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-six")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
_archive=abseil-py-pypi-v${pkgver}
source=(${_archive}.tar.gz::https://github.com/abseil/abseil-py/archive/pypi-v${pkgver}.tar.gz)
sha256sums=('0a145cb81101d1add8b87eaae58c5d51521084bf7cc4e4654928b326a864c6c3')

build() {
  cd "${srcdir}/${_archive}"

  ${MINGW_PREFIX}/bin/python3 setup.py --quiet build
}

package() {
  cd "${srcdir}/${_archive}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
