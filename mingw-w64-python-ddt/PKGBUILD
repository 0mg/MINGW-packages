# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=ddt
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.2.1
pkgrel=3
pkgdesc="Data-Driven/Decorated Tests (mingw-w64)"
arch=('any')
url='https://github.com/txels/ddt'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose"
              "${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-yaml")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/txels/ddt/archive/${pkgver}.tar.gz")
sha512sums=('c40114109fb99577a9572157e0cec7011efb3415b80874d0038949febf6c2d31ff7977c953e7c23770bc965541f79c7015826c1c7f39c2606db39ad5f38d60ad')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/nosetests3
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.md"
}

