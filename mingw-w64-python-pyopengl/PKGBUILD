# Maintainer: Antoine Martin <totaam@xpra.org>

_realname=pyopengl
_pyname=PyOpenGL
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_pyname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_pyname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_pyname}")
pkgver=3.1.4
pkgrel=1
pkgdesc="PyOpenGL is the most common cross platform Python binding to OpenGL and related APIs (mingw-w64)"
arch=('any')
url='http://pyopengl.sourceforge.net'
license=('PyOpenGL')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('staticlibs' 'strip' '!debug')
_dtoken='d7/8a/5db9096aa6506e405309c400bd0feb41997689cbba30683479c30dba6355'
_acceltoken='a3/43/ab02c99786e14280ab937d0f954b1c229e29c310a5e88c7a79d5f04490f0'
source=("https://files.pythonhosted.org/packages/${_dtoken}/${_pyname}-${pkgver}.tar.gz"
        "https://files.pythonhosted.org/packages/${_acceltoken}/${_pyname}-accelerate-${pkgver}.tar.gz")
sha512sums=('daddd91d6e4e5c731cb335c2a780388fbbc6829d819275d3daedea84f4c75e94de25608e0297abc339250c6dc11431ca653777287c2ca098b3e7f64473eac16e'
            'ead92c4ac8047379e8cbde4189e04ef0e25265f472c3eac089a81ebad271fba6f646cfdd43402b21966de5ee20ccab7e27faf94771adb1a93ccad8a3eaf610e8')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH} | true
  cp -r "${_pyname}-${pkgver}" "python-build-${CARCH}"
  cp -r "${_pyname}-accelerate-${pkgver}" "python-build-${CARCH}-accelerate"
  echo "TODO" > "python-build-${CARCH}/readme.rst"
  echo "TODO" > "python-build-${CARCH}-accelerate/readme.rst"
}

build() {
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
  msg "accelerate module:"
  cd "${srcdir}/python-build-${CARCH}-accelerate"
  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py check
  cd "${srcdir}/python-build-${CARCH}-accelerate"
  ${MINGW_PREFIX}/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"

  cd "${srcdir}/python-build-${CARCH}-accelerate"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
