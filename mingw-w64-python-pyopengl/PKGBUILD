# Maintainer: Antoine Martin <totaam@xpra.org>

_realname=PyOpenGL
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.1.3rc1
pkgrel=1
pkgdesc="PyOpenGL is the most common cross platform Python binding to OpenGL and related APIs (mingw-w64)"
arch=('any')
url='http://pyopengl.sourceforge.net'
license=('PyOpenGL')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-cython2"
             "${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
_dtoken='df/fe/b9da75e85bcf802ed5ef92a5c5e4022bf06faa1d41b9630b9bb49f827483'
_acceltoken='84/74/b48e413c97cbe51d778cba58a431bb8348d826a1576ab48b3c5340628bd8'
source=("https://files.pythonhosted.org/packages/95/32/f655c15deada40134cb2e56037ec13968fe5b1fedafe71d5026aa1d64f41/${_realname}-${pkgver}.tar.gz"
        "https://files.pythonhosted.org/packages/81/61/252f8d3f0736964b55e565b550cf9f2912f7363269ce9a26402a0ff06cf1/${_realname}-accelerate-${pkgver}.tar.gz")
sha512sums=('f9f446cdf06bcb10420c90e668ef77c7207ebc3c9401da2a8923ca2cb3941493ad3acfd49038bd923eadad7854e01b594843bfe9f2bb1af40f06c42987792687'
            '2b458da701178ab6a21467cf4291aaf7517f281162ad60cf4691d6cead39f0af3183de0e70a3fedff1f62eadd776860f5b91d8fbcfcaefc2f30b1e51fabc2b20')

prepare() {
  cd "${srcdir}"
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
    cp -r "${_realname}-accelerate-${pkgver}" "${builddir}-accelerate"
    echo "TODO" > "${builddir}/readme.rst"
    echo "TODO" > "${builddir}-accelerate/readme.rst"
  done
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
    msg "accelerate module:"  
    cd "${srcdir}/python${pver}-build-${CARCH}-accelerate"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py check
    cd "${srcdir}/python${pver}-build-${CARCH}-accelerate"
    ${MINGW_PREFIX}/bin/python${pver} setup.py check
  done
}

package_python3-PyOpenGL() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"

  cd "${srcdir}/python3-build-${CARCH}-accelerate"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-PyOpenGL() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/COPYING"

  cd "${srcdir}/python2-build-${CARCH}-accelerate"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_mingw-w64-i686-python2-PyOpenGL() {
  package_python2-PyOpenGL
}

package_mingw-w64-i686-python3-PyOpenGL() {
  package_python3-PyOpenGL
}

package_mingw-w64-x86_64-python2-PyOpenGL() {
  package_python2-PyOpenGL
}

package_mingw-w64-x86_64-python3-PyOpenGL() {
  package_python3-PyOpenGL
}
