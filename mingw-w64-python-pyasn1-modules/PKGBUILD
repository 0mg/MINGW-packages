# Maintainer: Frederic Wang <fred.wang@free.fr>

_realname=pyasn1-modules
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.2.7
pkgrel=2
pkgdesc='A collection of ASN.1-based protocols modules (mingw-w64)'
url='https://github.com/etingof/pyasn1-modules/'
license=('BSD')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-python3-pyasn1")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=(${_realname}-${pkgver}.tar.gz::"https://pypi.io/packages/source/p/pyasn1-modules/${_realname}-${pkgver}.tar.gz")
sha256sums=('0c35a52e00b672f832e5846826f1fb7507907f7d52fba6faa9e3c4cbe874fe4b')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
