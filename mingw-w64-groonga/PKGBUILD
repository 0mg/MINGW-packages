# Maintainer: Hiroshi Hatake <cosmo0920.wp@gmail.com>

_realname=groonga
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=9.0.1
pkgrel=1
pkgdesc="An opensource fulltext search engine."
arch=('any')
url="http://groonga.org/"
license=('LGPL2')
options=('strip' 'staticlibs')
source=("http://packages.groonga.org/source/${_realname}/${_realname}-$pkgver.tar.gz")
depends=("${MINGW_PACKAGE_PREFIX}-arrow"
         "${MINGW_PACKAGE_PREFIX}-libevent"
         "${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-msgpack-c"
         "${MINGW_PACKAGE_PREFIX}-onigmo"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-zeromq"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
sha256sums=('f1b14f0fd62ca7da550c11e164edcbcac7d83de75474cc4d3a6574adf2733507')

build() {
    cd $srcdir/${_realname}-$pkgver
    ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --disable-libedit \
      --with-onigmo=system \
      --without-mecab \
      --without-kytea \
      --without-cutter \
      --disable-benchmark \
      --disable-groonga-httpd \
      --enable-mruby

    make
}

package() {
    cd $srcdir/${_realname}-$pkgver
    make DESTDIR="$pkgdir" install

    local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
    pushd "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig"
    for pc in *.pc; do
      sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i $pc
    done
    popd
}
