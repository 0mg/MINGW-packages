# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=statsmodels
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-$_realname")
pkgver=0.10.1
pkgrel=4
pkgdesc="Statistical computations and models for use with SciPy (mingw-w64)"
arch=('any')
url="https://statsmodels.sourceforge.io/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3-scipy"
         "${MINGW_PACKAGE_PREFIX}-python3-pandas"
         "${MINGW_PACKAGE_PREFIX}-python3-patsy")
optdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose: needed for integrated test suite"
            "${MINGW_PACKAGE_PREFIX}-python3-matplotlib: needed for plotting functions")
makedepends=("${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        001-dont-include-extra-files.patch)
sha256sums=('320659a80f916c2edf9dfbe83512d9004bb562b72eedb7d9374562038697fa10'
            'd6ca9de4dbf5a8a578b582ddab7b0931ed037470b04b5a3c0ed8056ace94e257')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/001-dont-include-extra-files.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --skip-build \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.txt"
}
