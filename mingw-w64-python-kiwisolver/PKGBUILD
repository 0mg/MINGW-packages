# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=kiwisolver
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.1.0
pkgrel=3
pkgdesc="A fast implementation of the Cassowary constraint solver (mingw-w64)"
arch=('any')
url='https://github.com/nucleic/kiwi'
license=('PerlArtistic')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/nucleic/kiwi/archive/${pkgver}.tar.gz")
sha256sums=('48168c0ace18319c649e31907f6d8586f648e503560f506f128798b99393bcdb')

build() {
  cd "$srcdir/kiwi-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "$srcdir/kiwi-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
