# Maintainer: Saul Ibarra Corretge <saghul@gmail.com>

_realname=pyqt5
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-common")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-common")
pkgver=5.13.2
pkgrel=2
pkgdesc="Qt5 bindings for Python (mingw-w64)"
arch=('any')
license=('GPL')
url="https://riverbankcomputing.com/software/pyqt"
depends=("${MINGW_PACKAGE_PREFIX}-python3-sip"
         "${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-qtwebkit")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
source=(https://www.riverbankcomputing.com/static/Downloads/PyQt5/${pkgver}/PyQt5-${pkgver}.tar.gz
        001-mingw-python.patch)
sha256sums=('adc17c077bf233987b8e43ada87d1e0deca9bd71a13e5fd5fc377482ed69c827'
            '7d950551cbe39d0f9d191def17bd05c430525a5a80e9cc491d1f22c2d786fb47')

prepare() {
 cd "${srcdir}"/PyQt5-${pkgver}

  #Apply patches
  patch -p1 -i "${srcdir}"/001-mingw-python.patch
}

build() {
  [[ -d python3-${MINGW_CHOST} ]] && rm -rf python3-${MINGW_CHOST}
  cp -r PyQt5-${pkgver} python3-${MINGW_CHOST}
  pushd python3-${MINGW_CHOST} > /dev/null

    ${MINGW_PREFIX}/bin/python3 configure.py \
      --confirm-license \
      --qsci-api \
      --no-dist-info \
      --sipdir ${MINGW_PREFIX}/share/sip/PyQt5 \
      -q ${MINGW_PREFIX}/bin/qmake.exe \
      --verbose
    make V=1
  popd > /dev/null
}

package(){
  cd python3-${MINGW_CHOST}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}
