# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=urllib3
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.25.6
pkgrel=2
pkgdesc="HTTP library with thread-safe connection pooling and file post support (mingw-w64)"
url='https://github.com/shazow/urllib3'
license=('MIT')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-certifi"
         "${MINGW_PACKAGE_PREFIX}-python3-idna")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-ndg-httpsclient"
             "${MINGW_PACKAGE_PREFIX}-python3-pyasn1"
             "${MINGW_PACKAGE_PREFIX}-python3-pyopenssl"
             #"${MINGW_PACKAGE_PREFIX}-python3-pysocks"
             "${MINGW_PACKAGE_PREFIX}-python3-mock")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose"
              "${MINGW_PACKAGE_PREFIX}-python3-tornado"
              "${MINGW_PACKAGE_PREFIX}-python3-coverage"
              "${MINGW_PACKAGE_PREFIX}-python3-psutil")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/shazow/urllib3/archive/${pkgver}.tar.gz")
sha256sums=('066fa334d4b446ea2b5edf2f8b392c65993de604e18d708b0aca9e34dd2e6836')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} python3-build-${CARCH}
}

build() {
  cd "${srcdir}"
  msg "Python 3 build for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd ${srcdir}/python3-build-${CARCH}
  ${MINGW_PREFIX}/bin/python3 setup.py build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  install -D -m644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}"/share/licenses/python3-${_realname}/LICENSE.txt
}
