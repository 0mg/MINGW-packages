# Maintainer: AlexWMF <alexxwmf@gmail.com>

_realname=gst-libav
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.16.0
pkgrel=2
pkgdesc="GStreamer libav (mingw-w64)"
arch=('any')
url="https://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-nasm")
depends=("${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg")
options=(!libtool strip staticlibs)
source=(${url}/src/${_realname}/${_realname}-${pkgver}.tar.xz
        'link-bcrypt.patch')
sha256sums=('dfac119043a9cfdcacd7acde77f674ab172cf2537b5812be52f49e9cddc53d9a'
            'ab8f22c7e1ce4c9d268ac6f950b09fc344012bc13d35c62c8881dbf40ecf1acc')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -Np1 -i "${srcdir}"/link-bcrypt.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYSTEM=mingw32 \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-package-origin=https://www.msys2.org \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc \
    --with-system-libav
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
