# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>.

_realname=oslotest
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.8.1
pkgrel=2
pkgdesc="Oslo test framework (mingw-w64)"
arch=('any')
url='https://github.com/openstack/oslotest'
license=('Apache')
validpgpkeys=('gpg_KEY')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pbr"
             "${MINGW_PACKAGE_PREFIX}-python3-fixtures"
             "${MINGW_PACKAGE_PREFIX}-python3-subunit"
             "${MINGW_PACKAGE_PREFIX}-python3-stestr"
             "${MINGW_PACKAGE_PREFIX}-python3-testscenarios"
             "${MINGW_PACKAGE_PREFIX}-python3-testtools"
             "${MINGW_PACKAGE_PREFIX}-python3-mock"
             "${MINGW_PACKAGE_PREFIX}-python3-mox3"
#             "${MINGW_PACKAGE_PREFIX}-python3-os-client-config"
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector")
install=${_realname}-${CARCH}.install
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/openstack/oslotest/archive/${pkgver}.tar.gz")
sha512sums=('97959cac5e80e5fd9de08ae02ac0e0b5e2373c125abd386c1c7c532be4caeafd0faeb66831e23e3adf40f6994c2b78f0333043aa596759ea39226c806d81f595')


# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  export PBR_VERSION=${pkgver}
}

build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  msg "Python test for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  PYTHON=${MINGW_PREFIX}/bin/python ${MINGW_PREFIX}/bin/stestr run
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"

  # This entire section should be removed if the package does NOT install
  # anything in the /mingw*/bin directory.
  ### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
      -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
      -i "${pkgdir}${MINGW_PREFIX}/bin/oslo_run_pre_release_tests"
  #### end section ####
}
