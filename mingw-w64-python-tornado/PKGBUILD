# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=tornado
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=6.0.3
pkgrel=3
pkgdesc="open source version of the scalable, non-blocking web server and tools (mingw-w64)"
arch=('any')
url='https://www.tornadoweb.org/'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/tornadoweb/tornado/archive/v${pkgver}.tar.gz")
sha256sums=('a97ac3b8c95867e534b48cb6fbbf156f5ca5b20c423bb06894c17b240d7a18fc')

noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  # work around an issue with symbolic links in the tarball.
  bsdtar zxf ${_realname}-${pkgver}.tar.gz || true

  rm -rf python3-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python3-build-${CARCH}"

  export TORNADO_EXTENSION=1
}

build() {
  msg "Python 3 build for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

# We probably will need various prerequisits for this
#check() {
#  msg "Python 3 test for ${CARCH}"
#  cd "${srcdir}/python3-build-${CARCH}"
#  ${MINGW_PREFIX}/bin/python3 setup.py check
#}

package() {
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
