# Maintainer: Philippe Weyland <philippe.weyland@libertysurf.fr>

_realname=gmic
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.6.4
pkgrel=1
pkgdesc="A Full-Featured Open-Source Framework for Image Processing (mingw-w64)"
arch=(any)
url="https://gmic.eu"
license=("CeCILL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-fftw"
		"${MINGW_PACKAGE_PREFIX}-libpng"
		"${MINGW_PACKAGE_PREFIX}-libjpeg"
		"${MINGW_PACKAGE_PREFIX}-libtiff"
		"${MINGW_PACKAGE_PREFIX}-curl"
    "${MINGW_PACKAGE_PREFIX}-openexr"
		"${MINGW_PACKAGE_PREFIX}-opencv"
		)
source=("${_realname}-${pkgver}.tar.gz::https://gmic.eu/files/source/${_realname}_${pkgver}.tar.gz"
		"0001-gmic-2.6.4-windows-build.patch")
md5sums=('D796C8DCDB4C724DBB29A5CDE0A7566A'
		 '66FDBB04898D544B91E3BDF757D19276')
prepare() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  export PKG_CONFIG_PATH=/usr/lib/pkgconfig
  patch -p1 -i "${srcdir}/0001-gmic-2.6.4-windows-build.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  make cli lib
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  make -j1 DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
