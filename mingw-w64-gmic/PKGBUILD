# Maintainer: Philippe Weyland <philippe.weyland@libertysurf.fr>

_realname=gmic
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.6.4
pkgrel=1
pkgdesc="A Full-Featured Open-Source Framework for Image Processing (mingw-w64)"
arch=(any)
url="https://gmic.eu"
license=("CeCILL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-openexr"
         "${MINGW_PACKAGE_PREFIX}-opencv")
source=("${_realname}-${pkgver}.tar.gz::https://gmic.eu/files/source/${_realname}_${pkgver}.tar.gz"
        "0001-gmic-2.6.4-windows-build.patch")
sha256sums=('4cd88b2dca6b9b1a330ab4556d36656bafb98e4e9814bf0448545b27ef18dae3'
            '3f18aa5a20471e441c083c28de8afc6122d4c89aa86dd7391299efadc10b3420')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  patch -p1 -i "${srcdir}/0001-gmic-2.6.4-windows-build.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  make USR=${MINGW_PREFIX} cli lib
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  make -j1  USR=${MINGW_PREFIX} DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
