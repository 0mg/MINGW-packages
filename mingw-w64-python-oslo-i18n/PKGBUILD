# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-i18n
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.24.0
pkgrel=2
pkgdesc="Oslo i18n library (mingw-w64)"
arch=('any')
url='https://pypi.python.org/pypi/oslo.i18n/'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3" 
         "${MINGW_PACKAGE_PREFIX}-python3-six"
         "${MINGW_PACKAGE_PREFIX}-python3-pbr"
         "${MINGW_PACKAGE_PREFIX}-python3-babel")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/openstack/oslo.i18n/archive/${pkgver}.tar.gz")
sha512sums=('7734a46ec186073978d804a44ac8f97f474c5873e54e5443345db583f365627b7f0756cf559086dcc5b2612a4f7871491fac1029d85d1b0a3a9a1d6ab69fa628')

prepare() {
  cd "${srcdir}/oslo.i18n-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  # Set version for python_pbr
  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/oslo.i18n-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/oslo.i18n-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py check
}

package() {
  cd "${srcdir}/oslo.i18n-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
