# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=openmdao
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.9.1
pkgrel=2
pkgdesc="An open-source MDAO framework written in Python (mingw-w64)"
arch=('any')
license=('apache')
url="https://openmdao.org/"
depends=("${MINGW_PACKAGE_PREFIX}-python3-numpy"
         "${MINGW_PACKAGE_PREFIX}-python3-scipy"
         "${MINGW_PACKAGE_PREFIX}-python3-networkx"
         "${MINGW_PACKAGE_PREFIX}-python3-sqlitedict"
         "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
         "${MINGW_PACKAGE_PREFIX}-python3-six")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("OpenMDAO-${pkgver}.tar.gz::https://github.com/OpenMDAO/OpenMDAO/archive/${pkgver}.tar.gz")
sha256sums=('f9023ebfc78d69246cf258542194eea38b9bb4f6768a4ea85dc4b7e1d4577126')

prepare() {
  cd "${srcdir}/OpenMDAO-${pkgver}"

  sed -i "s|from distutils.core import setup|from setuptools import setup|" setup.py
}

build() {
  cd "${srcdir}/OpenMDAO-${pkgver}"

  python3 setup.py build
}

package() {
  cd "${srcdir}/OpenMDAO-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --root=${pkgdir} --prefix=${MINGW_PREFIX} --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
}
