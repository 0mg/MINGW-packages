# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=ifaddr
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.1.6
pkgrel=3
pkgdesc="Enumerates all IP addresses on all network adapters of the system. (mingw-w64)"
arch=('any')
url='https://github.com/pydron/ifaddr'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose")
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/pydron/ifaddr/archive/$pkgver.tar.gz")
sha256sums=('fb42188de577aa3acf14b06e9fcd770e755c7ced137db49191411956b75786ec')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/nosetests3 ${_realname}/test_ifaddr.py
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE/txt"
}
