# Contributor: Oleg A. Khlybov <fougas@mail.ru>

_realname=msmpi
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.1.1
pkgrel=1
pkgdesc="Microsoft MPI SDK (mingw-w64)"
arch=('any')
url="https://docs.microsoft.com/en-us/message-passing-interface/microsoft-mpi"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-binutils")
depends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gcc-fortran")
options=('strip')
source=(
  'mpi.c'
  'mpi.h'
  'mpif.h'
  'mpi.f90'
  'mpifptr.h.x86_64'
  'mpifptr.h.i686'
  'msmpi.def.x86_64'
  'msmpi.def.i686'
)
sha256sums=(
  '8f63f630d8f6e734914cf50166795395d33dcc1c03d80f39aced9fe1aff9577a'
  'cc23825faee71ffacf666300c375f4c73d605e72cd04310ce5efae778244fd7b'
  '46af38075602397da4380d28bd82f85d9a51f47ae8fb7b70ab50eb85b682eda1'
  '18cd772e2b88fe891e9c89ef3ba16b9d5a13c29e504ea0bfdcaaedc84b6526cc'
  'e1f280b0d5d8d770ae12488968e1ddecd6d09eb28a86194caf5285183e415a37'
  'a4bd0ed8711f7a51a67be1e512e69f37458c002ea9604a92ff1f78f6b91eab61'
  '137aa913e0ae0b3c60fa19fc673486305e96332f89eb3a7b0ba75b6eb6577918'
  '88cdb34ccde5ac09769ae71df27b20f8d590178c57cde131a2dd768735261663'
)

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  mkdir -p bin include lib
  # Import library
  ${MINGW_PREFIX}/bin/dlltool -k -d "$srcdir/msmpi.def.${CARCH}" -l lib/libmsmpi.a
  # Headers
  cp $srcdir/{mpi.h,mpif.h,mpi.f90} include
  cp $srcdir/mpifptr.h.${CARCH} include/mpifptr.h
  # Compiler wrappers
  mpic="$srcdir/mpi.c"
  cflags="-s -O2 -DNDEBUG"
  gcc ${cflags} -o bin/mpicc.exe -DCC "${mpic}"
  gcc ${cflags} -o bin/mpicxx.exe -DCXX "${mpic}"
  cp bin/mpicxx.exe bin/mpic++.exe
  gcc ${cflags} -o bin/mpifort.exe -DFC "${mpic}"
  cp bin/mpifort.exe bin/mpif77.exe
  cp bin/mpifort.exe bin/mpif90.exe
}

package() {
  cd "${srcdir}/build-${CARCH}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig"
  cp -r * "${pkgdir}${MINGW_PREFIX}"
  echo "
    prefix=${MINGW_PREFIX}
    libdir=\${prefix}/lib
    includedir=\${prefix}/include
    Name: ${_realname}
    URL: ${url}
    Version: ${pkgver}
    Description: ${pkgdesc}
    Cflags: -I\${includedir}
    Libs: -L\${libdir} -l${_realname}
  " | sed '/^\s*$/d;s/^\s*//' > "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${_realname}.pc"
}
