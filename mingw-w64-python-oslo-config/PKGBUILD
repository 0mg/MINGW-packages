# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-config
pkgbase=mingw-w64-python-${_realname}
pkgname=( "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=6.11.1
pkgrel=3
pkgdesc="parsing command line arguments and .ini style configuration files (mingw-w64)"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config"
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-six"
         "${MINGW_PACKAGE_PREFIX}-python3-netaddr"
         "${MINGW_PACKAGE_PREFIX}-python3-stevedore"
         "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
         "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
         "${MINGW_PACKAGE_PREFIX}-python3-rfc3986"
         "${MINGW_PACKAGE_PREFIX}-python3-yaml")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-sphinx"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python3-oslo-log")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/openstack/oslo.config/archive/${pkgver}.tar.gz")
sha512sums=('ea01bcd83900f368f7df29c841ed17bfd8baad1d011da83287111d36ca45d7e00647ecd68c40e34d61f5d0bb07eeb4313c6fcb41efae102110593f81a7ea7242')

prepare() {
  cd "${srcdir}/oslo.config-${pkgver}"

  sed -i '/argparse/d' requirements.txt

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/oslo.config-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/oslo.config-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py testr
}

package() {
  #This package install is needed for .fixups with .EXE's 
  #in the bit directory.  The install files "python-exe-installs"
  #and should be renamed to your _realname .
  install=${_realname}3-${CARCH}.install

  cd "${srcdir}/oslo.config-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"

# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory.
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
#### end section ####
}
