# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=theano
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.0.4
pkgrel=5
pkgdesc="Python library that allows you to define, optimize, and evaluate mathematical expressions involving multi-dimensional arrays efficiently (mingw-w64)"
arch=('any')
url="https://github.com/Theano/Theano"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-numpy"
         "${MINGW_PACKAGE_PREFIX}-python3-scipy"
         "${MINGW_PACKAGE_PREFIX}-python3-six")
optdepends=("${MINGW_PACKAGE_PREFIX}-gcc: Support to execute optimized C-implementations (for both CPU and GPU)")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
_archive=Theano-rel-${pkgver}
source=(${_archive}.tar.gz::https://github.com/Theano/Theano/archive/rel-${pkgver}.tar.gz
        0001-cmodule-obtain-mingw-w64-python-library.patch
        0002-not-use-np.distutils.__config__.blas_opt_info.patch)
sha256sums=('deb711bbb8a8f6021d4ad6f255a890df51507cb1111dd3fc471ef40f23f7b88d'
            'fd4a66aac925abdd9316bb142784c97e63d42ef590dddddf5d1771e69ea189df'
            '532ed23eadd9623fb87c7c50e25f64d2b68fd24cb4b9f546be2a154685b5799a')
noextract=(${_archive}.tar.gz)

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -b -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"
  bsdtar zxf ${_archive}.tar.gz || true

  cd "${srcdir}/${_archive}"
  apply_patch_with_msg \
    0001-cmodule-obtain-mingw-w64-python-library.patch \
    0002-not-use-np.distutils.__config__.blas_opt_info.patch
}

build() {
  cd "${srcdir}/${_archive}"

  ${MINGW_PREFIX}/bin/python3 setup.py --quiet build
}

package() {
  install=${_realname}3-${CARCH}.install

  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})

  cd "${srcdir}/${_archive}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done

  for f in theano-cache theano-nose ; do
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,3}.exe
    if [ -f "${pkgdir}${MINGW_PREFIX}"/bin/${f}.exe.manifest ]; then
      mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,3}.exe.manifest
      sed -e "s|${f}|${f}3|g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${f}3.exe.manifest
    fi
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,3}-script.py
  done

  install -Dm644 doc/LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
