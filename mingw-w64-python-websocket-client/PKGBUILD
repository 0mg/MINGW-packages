# Maintainer: Smo <smo@xpra.org>

_realname=websocket-client
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.56.0
pkgrel=3
pkgdesc="websocket client for python (mingw-w64)"
arch=('any')
url='https://github.com/websocket-client/websocket-client'
license=('LGPL-2.1')
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-six")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=(#"${_realname}-${pkgver}.tar.gz"::"https://github.com/websocket-client/websocket-client/archive/v${pkgver}.tar.gz"
        "https://pypi.io/packages/source/w/${_realname}/${_realname//-/_}-${pkgver}.tar.gz")
sha256sums=('1fd5520878b68b84b5748bb30e592b10d0a91529d5383f74f4964e72b297fd3a')

prepare() {
  cd "${srcdir}/${_realname//-/_}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname//-/_}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname//-/_}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py test
}

package() {
  cd "${srcdir}/${_realname//-/_}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
}
