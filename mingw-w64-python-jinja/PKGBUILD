# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=jinja
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.10.3
pkgrel=2
pkgdesc="A simple pythonic template language written in Python (mingw-w64)"
arch=('any')
license=('BSD')
url="http://jinja.pocoo.org/"
depends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
         "${MINGW_PACKAGE_PREFIX}-python3-markupsafe")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/pallets/jinja/archive/${pkgver}.tar.gz)
sha256sums=('db49236731373e4f3118af880eb91bb0aa6978bc0cf8b35760f6a026f1a9ffc4')

prepare() {
  cd "${srcdir}"
  rm -rf python3-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python3-build-${CARCH}"
}

build() {
  msg "Python 3 build for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.rst"
}
