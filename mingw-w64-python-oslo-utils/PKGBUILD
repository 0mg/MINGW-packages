# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-utils
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.41.2
pkgrel=2
pkgdesc="Oslo Utility library (mingw-w64)"
arch=('any')
url='https://pypi.org/project/oslo.utils/'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pbr"
             "${MINGW_PACKAGE_PREFIX}-python3-iso8601"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python3-pytz"
             "${MINGW_PACKAGE_PREFIX}-python3-netaddr"
             "${MINGW_PACKAGE_PREFIX}-python3-netifaces"
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-ddt"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/openstack/oslo.utils/archive/${pkgver}.tar.gz")
sha512sums=('cef8a9c49e60b17053960618a1bb62af4ae752b58c80403a0d17711e5d5d998b980bf00fc3c0f53180379d2edd230a666d2ce76c44ba88abf6add6355fe6f97d')

prepare() {
  cd "${srcdir}/oslo.utils-${pkgver}"

  # Use python 3's standard monotonic function
  sed -i '/monotonic/d' requirements.txt
  sed -i 's/from monotonic import monotonic/from time import monotonic/' oslo_utils/timeutils.py

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/oslo.utils-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/oslo.utils-${pkgver}"

  ${MINGW_PREFIX}/bin/stestr run
}

package() {
  cd "${srcdir}/oslo.utils-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

