# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=xlsxwriter
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.2.2
pkgrel=2
pkgdesc="A Python module for creating Excel XLSL files (mingw-w64)"
url="https://github.com/jmcnamara/XlsxWriter"
arch=('any')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/jmcnamara/XlsxWriter/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('227279fa4389808b8b894830dfc4e0ae3f3055e81767677a24ee8da03527a98e')

build() {
  cd "${srcdir}/XlsxWriter-RELEASE_${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/XlsxWriter-RELEASE_${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE

  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})
  for filename in vba_extract; do
    sed -e "s|${_mingw_prefix}/bin/|/usr/bin/env |g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${filename}.py
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${filename}.py "${pkgdir}${MINGW_PREFIX}"/bin/${filename}-py3.py
  done
}
