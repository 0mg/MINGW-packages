# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=freeglut
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pre=
_ver=3.2.0
pkgver=${_ver}${_pre}
pkgrel=1
pkgdesc="Freeglut allows the user to create and manage windows containing OpenGL contexts (mingw32-w64)"
arch=('any')
url="https://freeglut.sourceforge.io/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
options=('strip' '!libtool' 'staticlibs')
source=("https://downloads.sourceforge.net/freeglut/${_realname}-${_ver}${_pre}.tar.gz"
        "0002-fix-version-and-install-glut-h.patch")
sha256sums=('0208ef9fce43a7f52482a20a24f7802f654cd472e9fda52e1e508d6668230ec1'
            '7ae6e394bb12b4afdbbbc00e669b4679b1f0043e5a5156197c889e52f5bf3fcd')
noextract=(${_realname}-${_ver}${_pre}.tar.gz)

prepare() {
  # Clean up old sources so re-patching doesn't fail.
  [[ -d ${srcdir}/${_realname}-${_ver}${_pre} ]] && rm -rf ${srcdir}/${_realname}-${_ver}${_pre}
  tar -xzvf ${_realname}-${_ver}${_pre}.tar.gz -C ${srcdir}
  # Upstreaming at https://github.com/dcnieho/FreeGLUT/pull/41
  cd ${_realname}-${_ver}${_pre}
  patch -p1 -i "${srcdir}"/0002-fix-version-and-install-glut-h.patch
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-${_ver}${_pre}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
