# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=traitlets
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=4.3.3
pkgrel=2
pkgdesc="A lightweight Traits like module (mingw-w64)"
url="https://github.com/ipython/traitlets"
arch=('any')
license=('Modified BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3-ipython_genutils"
         "${MINGW_PACKAGE_PREFIX}-python3-decorator")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/ipython/${_realname}/archive/${pkgver}.tar.gz")
sha256sums=('b686c1aadf6ee5a9ee4c22df23bc5cd5bb7b5cfa18afe092e0a139cc2f05fe2e')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python3 -m pytest "${_realname}/tests"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --root=${pkgdir} --prefix=${MINGW_PREFIX} --optimize=1
  install -Dm644 COPYING.md ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE
}
