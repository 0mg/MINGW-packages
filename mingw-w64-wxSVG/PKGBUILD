# Maintainer: Jordan Irwin <antumdeluge@gmail.com>

_realname=wxsvg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgbase="mingw-w64-${_realname}"
pkgver=1.5.19
pkgrel=3
pkgdesc="A C++ library to create, manipulate and render SVG files (mingw-w64)"
arch=("any")
url="http://wxsvg.sourceforge.net/"
license=("custom:wxWindows")
depends=("${MINGW_PACKAGE_PREFIX}-wxWidgets"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg")
makedepends=("bsdtar"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=("https://downloads.sourceforge.net/project/wxsvg/wxsvg/${pkgver}/wxsvg-${pkgver}.tar.bz2"
        "001-wxsvg-1.5.19-pkg-config.patch")
sha256sums=('3f78783fb1ea8242cc3f5f4d9b5ca8c79ea90f0837849f5a908478e13a560f9e'
            '1138bfac5305a1dc805f43821796f733b275fde9cd8a9855308a962761be3baa')
extractdir="${_realname}-${pkgver}"
builddir="build-${CARCH}"

prepare() {
 cd "${srcdir}/${extractdir}"
 patch -p1 -i "${startdir}/001-wxsvg-1.5.19-pkg-config.patch"
}

build() {
  [[ -d "${srcdir}/${builddir}" ]] && rm -rf "${srcdir}/${builddir}"
  mkdir -p "${srcdir}/${builddir}" && cd "${srcdir}/${builddir}"

  ../${extractdir}/configure \
      --prefix=${MINGW_PREFIX} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --build=${MINGW_CHOST} \
      --enable-shared \
      --enable-static
  make
}

check() {
  cd "${srcdir}/${builddir}"
  make -k check
}

package() {
  cd "${srcdir}/${builddir}"
  make install DESTDIR="${pkgdir}"

  # license
  install -Dm644 "${srcdir}/${extractdir}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
