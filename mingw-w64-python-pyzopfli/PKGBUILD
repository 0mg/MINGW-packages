# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pyzopfli
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.1.6
_zopfliver=1.0.2
pkgrel=3
pkgdesc="Python bindings for the Zopfli compressor (mingw-w64)"
arch=('any')
url='https://github.com/obp/py-zopfli/'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/obp/py-zopfli/archive/v${pkgver}.tar.gz"
        "zopfli-${_zopfliver}.tar.gz"::"https://github.com/google/zopfli/archive/zopfli-${_zopfliver}.tar.gz"
        win32-fixes.patch)
sha256sums=('29e2327a2f698d05cb361e517179d6e2cacf84228894d92b9d90539c4bc383c0'
            '4a570307c37172d894ec4ef93b6e8e3aacc401e78cbcc51cf85b212dbc379a55'
            '25c2734059f3c794a8dddd5e9c7c176d96ff6f8d0ac0dada8d750951a995b41e')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp0 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
 #for this, you have to something weird, extract zopfli into a dir
 #named zopfli py-zopfli dir.  We also have to patch zopfli to match what
#is in the mingw-w64-zopfli because that gives a serious warning about Win32.
  rm -rf py-zopfli-${pkgver}/zopfli
  apply_patch_with_msg win32-fixes.patch
  cp -r "zopfli-zopfli-${_zopfliver}" "py-zopfli-${pkgver}/zopfli/"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/py-zopfli-${pkgver}"

  ${MINGW_PREFIX}/bin/python${pver} setup.py build
}

check() {
  cd "${srcdir}/py-zopfli-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py check
}

package() {
  cd "${srcdir}/py-zopfli-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
