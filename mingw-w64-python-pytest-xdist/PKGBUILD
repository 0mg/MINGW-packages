# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-xdist
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.30.0
pkgrel=1
pkgdesc="py.test xdist plugin for distributed testing and loop-on-failing modes (mingw-w64)"
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-pytest-forked" 
         "${MINGW_PACKAGE_PREFIX}-python3-execnet")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest"
             "${MINGW_PACKAGE_PREFIX}-python2-filelock"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools-scm")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/pytest-dev/pytest-xdist/archive/v${pkgver}.tar.gz")
sha512sums=('ef1f9758884290ac1518ce7483f536ef2356c8c9e8ed2b480de14e61ebc26b4bdd09d4cecdad84e3adfb9544b51ad9fce530fedecf1bac88cbc716e083c7f681')

prepare() {
  cd "${srcdir}"
  rm -rf python3-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python3-build-${CARCH}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python 3 build for ${CARCH}"  
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  # Hack entry points by installing it
  local OLD_PYTHON_PATH=$PYTHONPATH

  msg "Python 3 test for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --root="$PWD/tmp_install" --optimize=1 --skip-build
  _PATHONPATH1="${srcdir}/python3-build-${CARCH}/tmp_install${MINGW_PREFIX}/lib/python3.7/site-packages"
  _PATHONPATH2="${OLD_PYTHON_PATH};${srcdir}/python3-build-${CARCH}/tests"
  PYTHONPATH="${_PATHONPATH1};${_PATHONPATH2}" py.test || true

}

package() {
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
