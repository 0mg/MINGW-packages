# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=sympy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.4
pkgrel=3
pkgdesc="Symbolic manipulation package (Computer Algebra System), written in pure Python (mingw-w64)"
arch=('any')
url='https://www.sympy.org/en/index.html'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-mpmath")
optdepends=("${MINGW_PACKAGE_PREFIX}-python3-ipython: user friendly interface for isympy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz::https://github.com/sympy/sympy/archive/sympy-$pkgver.tar.gz")
sha512sums=('fdf2113478393ac71c08a440c97ded1d11e2cb630da237a4e32d03a54fda1bb51c6317a757e5d51683fc1ff7ed865cf3b4c02927bec5974eb68bacc5b88f865a')

prepare() {
  cd "${srcdir}/${_realname}-${_realname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py test || warning "Tests failed"
}

package() {
  install=${_realname}3-${CARCH}.install

  cd "${srcdir}/${_realname}-${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"

# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory.
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _ff in ${pkgdir}${MINGW_PREFIX}/bin/*.py; do
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_ff}
  done
#### end section ####
}
