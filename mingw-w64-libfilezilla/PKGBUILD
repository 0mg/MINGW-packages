# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=libfilezilla
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.17.1
pkgrel=1
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs (mingw-w64)"
arch=('any')
url="https://lib.filezilla-project.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-nettle"
         "${MINGW_PACKAGE_PREFIX}-gnutls")
makedepends=("${MINGW_PACKAGE_PREFIX}-gettext")
checkdepends=("${MINGW_PACKAGE_PREFIX}-cppunit")
license=('GPL')
source=("${_realname}-${pkgver}.tar.bz2"::"https://download.filezilla-project.org/${_realname}/${_realname}-${pkgver}.tar.bz2"
        "libfilezilla.rc"
        "manifest.xml"
        "0001-libfilezilla-0.17.0-define-version.patch")
sha256sums=('d3ef141b8e5816384f686bd6d67fddcfb04be1cd7804d47df8b4b8b81365d3b2'
            '10e25d2f6e01af0d14f3f6c7fb842699a6acd1214842a8158567cf174199156c'
            '687b11a79db3deaf4c3cb40fc452b56f0df8dff54068d5b7be9851912aad8f8a'
            'cceaa47f769bdb02da527cd976ed29531bfd1db3c241b730d60f81f03ea85cb6')

prepare() {
  # copy missing .rc and manifest files to source tree.
  cp -f ${srcdir}/libfilezilla.rc ${srcdir}/${_realname}-${pkgver}/lib/windows
  cp -f ${srcdir}/manifest.xml ${srcdir}/${_realname}-${pkgver}/lib/windows
  
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-libfilezilla-0.17.0-define-version.patch"
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check || true
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
