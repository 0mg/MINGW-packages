# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=regex
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2019.08.19
pkgrel=2
pkgdesc="Alternative python regular expression module. (mingw-w64)"
arch=('any')
url='https://www.regex.org/'
license=('Python')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("https://files.pythonhosted.org/packages/source/r/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('587b62d48ca359d2d4f02d486f1f0aa9a20fbaf23a9d4198c4bed72ab2f6c849')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 -c 'from regex.test.test_regex import test_main; test_main()'
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  sed -n '1,/^$/p' ./regex_2/regex/regex.py > ./LICENSE
  install -Dm644 ./LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"

  install -v -m755 -d "${pkgdir}${MINGW_PREFIX}/share/doc/python3-regex"
  install -v -m644 ./docs/Features.html "${pkgdir}${MINGW_PREFIX}/share/doc/python3-regex/"
  install -v -m644 ./docs/Features.rst "${pkgdir}${MINGW_PREFIX}/share/doc/python3-regex/"
  install -v -m644 ./docs/UnicodeProperties.txt "${pkgdir}${MINGW_PREFIX}/share/doc/python3-regex/"
}
