# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=lz4
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9.0
pkgrel=1
pkgdesc="Very fast lossless compression algorithm (mingw-w64)"
arch=('any')
url="http://www.lz4.org/"
license=('BSD' 'GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
checkdepends=('diffutils')
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/lz4/lz4/archive/v${pkgver}.tar.gz"
        "001-mingw-install-over-msys.patch")
sha256sums=('f8b6d5662fa534bd61227d313535721ae41a68c9d84058b7b7d86e143572dcfb'
            '70d13c846295d407caa65c84d59bd5a5bf52c4ced183e8e6221c6499d84f8c11')

prepare() {
  cd ${_realname}-${pkgver}
  patch -bp1 -i ${srcdir}/001-mingw-install-over-msys.patch
  cd -

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
}

build() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 -C lib CC=${MINGW_PREFIX}/bin/gcc
  make -j1 -C programs CC=${MINGW_PREFIX}/bin/gcc lz4 lz4c
}

check() {
  cd ${srcdir}
  msg2 "rm -f profile.lz4"
  rm -f profile.lz4 || true

  ${srcdir}/build-${MINGW_CHOST}/programs/lz4 /etc/profile profile.lz4
  ${srcdir}/build-${MINGW_CHOST}/programs/lz4 -d profile.lz4 profile
  diff -q /etc/profile profile

  rm profile
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make CC=${MINGW_PREFIX}/bin/gcc DESTDIR="${pkgdir}" PREFIX=${MINGW_PREFIX} install
}
