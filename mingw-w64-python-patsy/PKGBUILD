# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=patsy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.5.1
pkgrel=3
pkgdesc="A Python package for describing statistical models and for building design matrices (mingw-w64)"
arch=('any')
url="https://github.com/pydata/patsy"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3-numpy")
optdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose: needed for integrated test suite"
            "${MINGW_PACKAGE_PREFIX}-python3-scipy: needed for spline-related functions")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("https://pypi.python.org/packages/source/p/patsy/${_realname}-${pkgver}.tar.gz")
sha256sums=('f115cec4201e1465cd58b9866b0b0e7b941caafec129869057405bfe5b5e3991')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root "${pkgdir}" --optimize=1

  install -Dm644 "LICENSE.txt" \
  "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
