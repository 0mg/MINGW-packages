# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pip
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=19.3.1
pkgrel=4
pkgdesc="An easy_install replacement for installing pypi python packages (mingw-w64)"
arch=('any')
license=('MIT')
url="https://pip.pypa.io/"
depends=("${MINGW_PACKAGE_PREFIX}-python3-appdirs"
         "${MINGW_PACKAGE_PREFIX}-python3-cachecontrol"
         "${MINGW_PACKAGE_PREFIX}-python3-colorama"
         "${MINGW_PACKAGE_PREFIX}-python3-contextlib2"
         "${MINGW_PACKAGE_PREFIX}-python3-distlib"
         "${MINGW_PACKAGE_PREFIX}-python3-html5lib"
         "${MINGW_PACKAGE_PREFIX}-python3-lockfile"
         "${MINGW_PACKAGE_PREFIX}-python3-msgpack"
         "${MINGW_PACKAGE_PREFIX}-python3-packaging"
         "${MINGW_PACKAGE_PREFIX}-python3-pep517"
         "${MINGW_PACKAGE_PREFIX}-python3-progress"
         "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
         "${MINGW_PACKAGE_PREFIX}-python3-pytoml"
         "${MINGW_PACKAGE_PREFIX}-python3-requests"
         "${MINGW_PACKAGE_PREFIX}-python3-retrying"
         "${MINGW_PACKAGE_PREFIX}-python3-six"
         "${MINGW_PACKAGE_PREFIX}-python3-webencodings")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx")
install=${_realname}3-${CARCH}.install
source=(${_realname}-${pkgver}.tar.gz::https://github.com/pypa/pip/archive/${pkgver}.tar.gz)
noextract=(${_realname}-${pkgver}.tar.gz)
sha256sums=('f12b7a6be2dbbfeefae5f14992c89175ef72ce0fe96452b4f66be855a12841ff')

shopt -s extglob

prepare() {
  # work around symlinks in pip package that can cause problems with some building
  # where dirs are not removed after being used.
  rm -rf ${_realname}-${pkgver}/tests/data/packages/symlinks/docs || true 2> /dev/nul
  bsdtar zxf ${_realname}-${pkgver}.tar.gz || true

  # specify that third-party stuff should not be bundled.  This is from Archlinux
  rm -rf ${_realname}-${pkgver}/src/pip/_vendor/!(__init__.py)
  sed -i -e 's/DEBUNDLED = False/DEBUNDLED = True/' \
         -e '/cachecontrol/a\    vendored("pep517")' \
        ${_realname}-${pkgver}/src/pip/_vendor/__init__.py

  rm -rf python3-build-${CARCH}| true
  cp -r "${_realname}-${pkgver}" "python3-build-${CARCH}"
}

build() {
  msg "Python 3 build for ${CARCH}" 
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  local _mingw_prefix=$(cygpath -wm ${MINGW_PREFIX})

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}" --optimize=1 --skip-build -v

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done

  install -D -m644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
