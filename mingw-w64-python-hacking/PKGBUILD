# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=hacking
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.1.0
pkgrel=3
pkgdesc="OpenStack Hacking Guideline Enforcement (mingw-w64)"
arch=('any')
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-pbr"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/openstack-dev/hacking/archive/${pkgver}.tar.gz")
sha512sums=('25e8fc8585b0de2c9f746a31539d2ee0b6b18685f17cac149bf8cf372d9c31d1f10ac93087d6bcc69b4d1a15b4b3c99cecbe15fe95e903c3de72aba21af32827')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  sed -e 's/flake8<2.7.0,/flake8/' -e 's/==/>=/' -i requirements.txt

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  # set version for python-pbr
  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py test || warning "Tests failed"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
