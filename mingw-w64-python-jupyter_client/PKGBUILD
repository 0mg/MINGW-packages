# Maintainer: Patrick Stewart <patstew@gmail.com>

_realname=jupyter_client
pkgbase=mingw-w64-python-jupyter_client
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.3.4
pkgrel=2
pkgdesc="The reference implementation of the Jupyter protocol (mingw-w64)"
arch=('any')
url="https://jupyter.org/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-ipykernel"
         "${MINGW_PACKAGE_PREFIX}-python3-jupyter_core"
         "${MINGW_PACKAGE_PREFIX}-python3-pyzmq")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/jupyter/jupyter_client/archive/${pkgver}.tar.gz")
sha256sums=('2af6f0e0e4d88009b11103490bea0bfb405c1c470e226c2b7b17c10e5dda9734')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  install=${_realname}3-${CARCH}.install

  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=0

  find "${pkgdir}/" -name "*.pyc" -delete
  find "${pkgdir}/" -type d -empty -delete

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*; do
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done

  install -Dm644 "COPYING.md" "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
