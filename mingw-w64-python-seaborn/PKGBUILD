# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=seaborn
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.9.0
pkgrel=3
pkgdesc="Statistical data visualization using matplotlib (mingw-w64)"
arch=('any')
url='https://stanford.edu/~mwaskom/software/seaborn/'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-pandas"
         "${MINGW_PACKAGE_PREFIX}-python3-matplotlib"
         )
optdepends=("${MINGW_PACKAGE_PREFIX}-python3-statsmodel: for some advanced statistical plots"
            "${MINGW_PACKAGE_PREFIX}-python3-beautifulsoup4: for getting dataset names"
            )
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-scipy"
             "${MINGW_PACKAGE_PREFIX}-python3-beautifulsoup4"
             )
source=("${_realname}-${pkgver}.tar.gz::https://github.com/mwaskom/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('e2910b86f719ceecd09350071cb68e7531d236d087d04515b961363dd7208e6f')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.txt"
}
