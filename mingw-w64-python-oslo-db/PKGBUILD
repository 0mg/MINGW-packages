# Maintainer: Some One <some.one@some.email.com>
#This value is here because some python package names have capital letters
#while there is a strong preference for lowercase letters in ArchLinux and
#MSYS packages.

_realname=oslo-db
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.0.2
pkgrel=2
pkgdesc="OpenStack Database Pattern Library (mingw-w64)"
arch=('any')
url='https://pypi.python.org/pypi/oslo.db'
license=('LICENSE')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-six"
         "${MINGW_PACKAGE_PREFIX}-python3-pbr"
         "${MINGW_PACKAGE_PREFIX}-python3-alembic"
         "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
         "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
         "${MINGW_PACKAGE_PREFIX}-python3-oslo-config"
         "${MINGW_PACKAGE_PREFIX}-python3-oslo-utils"
         "${MINGW_PACKAGE_PREFIX}-python3-sqlalchemy"
         "${MINGW_PACKAGE_PREFIX}-python3-sqlalchemy-migrate"
         "${MINGW_PACKAGE_PREFIX}-python3-stevedore")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python3-testresources"
              "${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-oslo-context")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/openstack/oslo.db/archive/${pkgver}.tar.gz")
sha512sums=('1e6317a51241bb0c0edc790fbf6095af4e6c52c5c92f3620f60b31a84a1f44ab5976ec2665832cb2b9759619828eee32eefcbbbb5f7e9f784cca591af0067dc1')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}/oslo.db-${pkgver}"

  sed -i 's/import unittest2/import unittest as unittest2/' oslo_db/tests/sqlalchemy/test_async_eventlet.py

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  # set version for PBR
  export PBR_VERSION=$pkgver
}

build() {
  cd "${srcdir}/oslo.db-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  cd "${srcdir}/oslo.db-${pkgver}"

  PYTHON=python3 ${MINGW_PREFIX}/bin/python3 setup.py testr || warning "tests failed"
}

package() {
  cd "${srcdir}/oslo.db-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
