# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=sqlitedict
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.6.0
pkgrel=3
pkgdesc="Persistent dict, backed by sqlite3 and pickle, multithread-safe (mingw-w64)"
arch=('any')
license=('BSD')
url="https://github.com/RaRe-Technologies/sqlitedict"
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-sqlite3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose"
              "${MINGW_PACKAGE_PREFIX}-python3-coverage")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/RaRe-Technologies/sqlitedict/archive/${pkgver}.tar.gz")
sha256sums=('8ab2e0cc361629f8eab8997930bb608b37bb22050b0304dbfa3b40ad7b278634')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python3 setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  nosetests3
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --root=${pkgdir} --prefix=${MINGW_PREFIX} --optimize=1

  install -D -m644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-sqlitedict/LICENSE"
}
